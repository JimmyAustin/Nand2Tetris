// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    Zero16(out=zeroPins);

    Mux16(a=x, b=zeroPins, sel=zx, out=postzxX);
    Not16(in=postzxX, out=negatedX);
    Mux16(a=postzxX, b=negatedX, sel=nx, out=calcedX);

    Mux16(a=y, b=zeroPins, sel=zy, out=postzyY);
    Not16(in=postzyY, out=negatedY);
    Mux16(a=postzyY, b=negatedY, sel=ny, out=calcedY);

    Add16(a=calcedX, b=calcedY, out=xyAdd);
    And16(a=calcedX, b=calcedY, out=xyAnd);

    Mux16(a=xyAnd, b=xyAdd, sel=f, out=calcedXy);

    Not16(in=calcedXy, out=negateCalcedXy);
    Mux16(a=calcedXy, b=negateCalcedXy, sel=no, out=result);

    // Calculate Out. The double negation is necessary because we can't use out downstream.
    // This could also be replaced with a Mux16, but that would be less efficient.
    Not16(in=result, out=nResult);
    Not16(in=nResult, out=out);

    // Calculate zr = (out == 0, 1, 0)
    Or16Way(in=result, out=nzr); // Calculate if there are any true pins
    Not(in=nzr, out=zr);

    // Calculate ng = (out < 0,  1, 0)
    MSB16(in=result, out=ng);
}